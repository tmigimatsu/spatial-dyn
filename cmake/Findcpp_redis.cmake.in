############################################################
# Finds the cpp_redis library built inside SpatialDyn
#
# Copyright 2018. All Rights Reserved.
#
# Created: October 19, 2018
# Authors: Toki Migimatsu
############################################################


find_package(PkgConfig)
pkg_check_modules(PC_cpp_redis REQUIRED IMPORTED_TARGET
    @CMAKE_BINARY_DIR@/cpp_redis/build/lib/pkgconfig/cpp_redis.pc)

set(cpp_redis_VERSION 4.3.1)
set(cpp_redis_INCLUDE_DIRS ${PC_cpp_redis_INCLUDE_DIRS})
find_library(cpp_redis_LIB cpp_redis "${PC_cpp_redis_LIBRARY_DIRS}")
find_library(tacopie_LIB tacopie "${PC_cpp_redis_LIBRARY_DIRS}")

if(PC_cpp_redis_FOUND AND NOT TARGET cpp_redis::cpp_redis)
    add_library(cpp_redis::cpp_redis INTERFACE IMPORTED)
    set(cpp_redis_LIBRARIES ${cpp_redis_LIB} ${tacopie_LIB} pthread)
    set_target_properties(cpp_redis::cpp_redis PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${cpp_redis_INCLUDE_DIRS}"
        INTERFACE_LINK_LIBRARIES "${cpp_redis_LIBRARIES}"
    )
endif()

mark_as_advanced(cpp_redis_VERSION cpp_redis_INCLUDE_DIRS cpp_redis_LIB tacopie_LIB)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(cpp_redis
    REQUIRED_VARS cpp_redis_INCLUDE_DIRS cpp_redis_LIB tacopie_LIB
    VERSION_VAR cpp_redis_VERSION
)

